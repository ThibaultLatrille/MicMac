import os
import numpy as np

ROOT = os.path.abspath('../../..')
FOLDER = os.path.abspath('.')

exec_dico = {}
for executable in ["nodetraits", "readnodetraits", 'rb']:
    exec_path = ""
    for b in ["BayesCode", "bayescode", "Bayescode", "bayesCode", "BAYESCODe", "BayesCODE"]:
        exec_path = os.path.join(ROOT,f'utils/{b}/bin/{executable}')
        if os.path.exists(exec_path):
            break
    if executable == "rb":
        exec_path = os.path.join(ROOT,f'utils/revbayes/projects/cmake/rb')
    if not os.path.exists(exec_path):
        # Find executable in the path using whereis. If not found, raise an error.
        split = os.popen(f'whereis {executable}').read().split()
        if len(split) > 1:
            exec_path = split[1].strip()
        else:
            pg = "RevBayes" if executable == 'rb' else "BayesCode"
            raise FileNotFoundError(f'{executable} not found. Please install {pg} and add it to your path.')
    exec_dico[executable] = exec_path
    print(f"Found {executable} at {exec_path}")

configfile: 'config.yaml'

REPLICATES = [int(i) for i in np.linspace(config["min_number_loci"],config["max_number_loci"],config["nb_genes"])]
SEEDS = [int(i) for i in np.linspace(config["min_seed"],config["max_seed"],config["nb_seeds"])]
SIMULATOR_PARAMS = {s: " ".join(['--{0} {1}'.format(k,v) for k, v in d.items() if k != "model"]) for s, d in
                    config["simulators"].items()}
for tag in ["population_size", "mutation_rate"]:
    tag_sigma = f"{tag}_brownian_sigma"
    if f"scaled_{tag_sigma}" not in config:
        continue
    print(f"Order of magnitude in change of {tag}: {config[f'scaled_{tag_sigma}']}")
    config["core"][f"{tag_sigma}"] = config[f"scaled_{tag_sigma}"] / np.sqrt(config["core"]['number_of_generations'])
    print(f"{tag_sigma}: {config['core'][f'{tag_sigma}']}")
SIMULATOR_PARAMS["core"] = " ".join(['--{0} {1}'.format(k,v) for k, v in config["core"].items()])
SEED_POP_SIZE = 42
SEED_MUT_RATE = 42
output_pdf = [f"{FOLDER}/results/inference_ML.tsv.gz"]
if 'bayes_inference' in config and config['bayes_inference']:
    output_pdf.append(f"{FOLDER}/results/inference_bayesian.tsv.gz")
    output_pdf.append(f"{FOLDER}/results/inference_RevBayes.tsv.gz")

rule all:
    input: output_pdf


def simulator_output(wildcards):
    return f"{FOLDER}/{wildcards.simulator}/replicate_{wildcards.scaling}_seed{wildcards.seed}"


def variance_env(nbr_loci, a, mut_rate, pop_size, h2):
    vG = 4 * float(a) ** 2 * float(nbr_loci) * float(mut_rate) * int(pop_size)
    h2 = float(h2)
    assert vG > 0
    assert 0 <= h2 <= 1
    return vG * (1 - h2) / h2


rule run_simulations:
    input:
        exec=lambda wildcards: f"{ROOT}/simulator/build/{config['simulators'][wildcards.simulator]['model']}",
        tree=f"{ROOT}/simulator/{config['tree']}"
    output:
        nhx=f"{FOLDER}/{{simulator}}/replicate_{{scaling}}_seed{{seed}}.nhx.gz"
    params:
        file=lambda wildcards: simulator_output(wildcards),
        folder=lambda wildcards: f"{FOLDER}/{wildcards.simulator}",
        simulator=lambda wildcards: "{0} {1}".format(SIMULATOR_PARAMS["core"],SIMULATOR_PARAMS[wildcards.simulator]),
        ve=lambda wildcards: variance_env(nbr_loci=wildcards.scaling,a=config["core"]["mutation_mean_effect_size"],
            mut_rate=config["core"]["mutation_rate_per_loci_per_generation"],pop_size=config["core"]["population_size"],
            h2=config["heritability"])
    shell:
        'mkdir -p {params.folder} && {input.exec} --tree {input.tree} {params.simulator} --variance_environment {params.ve} --number_loci {wildcards.scaling} --seed {wildcards.seed} --seed_pop_size {SEED_POP_SIZE} --seed_mut_rate {SEED_MUT_RATE} --output {params.file} && gzip {params.file}.nhx'

rule run_neutral_simulation:
    input:
        exec=lambda wildcards: f"{ROOT}/simulator/build/neutral",
        tree=f"{ROOT}/simulator/{config['tree']}"
    output:
        nhx=f"{FOLDER}/tree/neutral_tree.nhx.gz"
    params:
        file=f"{FOLDER}/tree/neutral_tree",
        folder=lambda wildcards: f"{FOLDER}/tree",
        simulator=lambda wildcards: "{0} {1}".format(SIMULATOR_PARAMS["core"],SIMULATOR_PARAMS["neutral"])
    shell:
        'mkdir -p {params.folder} && {input.exec} --tree {input.tree} {params.simulator} --variance_environment 0.0 --number_loci 50000 --seed 42 --seed_pop_size {SEED_POP_SIZE} --output {params.file} && gzip {params.file}.nhx'


rule plot_simulations_ML:
    input:
        exp=expand(rules.run_simulations.output.nhx,scaling=REPLICATES,seed=SEEDS,simulator=config["simulators"]),
        neutral_tree=rules.run_neutral_simulation.output.nhx,
        script=f"{ROOT}/scripts/plot_simulations_ML.py"
    output:
        tsv=f"{FOLDER}/results/inference_ML.tsv.gz"
    shell:
        'python3 {input.script} --folder {FOLDER} --neutral_tree {input.neutral_tree} --output {output.tsv}'


rule pre_processed_traits:
    input:
        script=f"{ROOT}/scripts/pre_processed_traits_simulations.py",
        nhx=rules.run_simulations.output.nhx,
        neutral_tree=rules.run_neutral_simulation.output.nhx
    output:
        tree=f"{FOLDER}/{{simulator}}/replicate_{{scaling}}_seed{{seed}}.tree",
        traits=f"{FOLDER}/{{simulator}}/replicate_{{scaling}}_seed{{seed}}.traits.tsv",
        var_within=f"{FOLDER}/{{simulator}}/replicate_{{scaling}}_seed{{seed}}.var_within.tsv"
    shell:
        'python3 {input.script} --input {input.nhx} --neutral_tree {input.neutral_tree} --tree {output.tree} --traitsfile {output.traits} --var_within {output.var_within}'


def chain_name(wildcards):
    return f"{FOLDER}/{wildcards.simulator}/inference_{wildcards.scaling}_seed{wildcards.seed}"


rule run_inference:
    input:
        exec=exec_dico['nodetraits'],
        tree=rules.pre_processed_traits.output.tree,
        traits=rules.pre_processed_traits.output.traits
    output:
        run=f"{FOLDER}/{{simulator}}/inference_{{scaling}}_seed{{seed}}.run"
    params:
        chain=lambda wildcards: chain_name(wildcards),
        until=f"-u {config['bayes_until']}"
    shell:
        '{input.exec} {params.until} --uniq_kappa --df 1 --tree {input.tree} --traitsfile {input.traits} {params.chain}'

rule read_inference:
    input:
        exec=exec_dico['readnodetraits'],
        inference=rules.run_inference.output.run,
        var_within=rules.pre_processed_traits.output.var_within
    output:
        tsv=f"{FOLDER}/{{simulator}}/read_{{scaling}}_seed{{seed}}.ratio.tsv"
    params:
        chain=lambda wildcards: chain_name(wildcards),
        burnin=config['bayes_burn_in']
    log:
        stdout=f"{FOLDER}/{{simulator}}/read_{{scaling}}_seed{{seed}}.log",
        stderr=f"{FOLDER}/{{simulator}}/read_{{scaling}}_seed{{seed}}.err"
    shell:
        '{input.exec} -b {params.burnin} --var_within {input.var_within} --output {output.tsv} {params.chain} 1> {log.stdout} 2> {log.stderr} && gzip -f {params.chain}.chain && gzip -f {params.chain}.trace'

rule plot_simulations_bayesian:
    input:
        script=f"{ROOT}/scripts/plot_simulations_bayesian.py",
        tsv=expand(rules.read_inference.output.tsv,scaling=REPLICATES,seed=SEEDS,simulator=config["simulators"])
    output:
        plot=f"{FOLDER}/results/inference_bayesian.tsv.gz"
    log:
        stdout=f"{FOLDER}/results/inference_bayesian.log"
    shell:
        'python3 {input.script} --folder {FOLDER} --output {output.plot} 1> {log.stdout}'


rule convert_to_RevBayes:
    input:
        script=f"{ROOT}/scripts/convert_to_nexus.py",
        tree=f"{ROOT}/simulator/{config['tree']}",
        traits=rules.pre_processed_traits.output.traits
    output:
        nexus_tree=f"{FOLDER}/RevBayes/{{simulator}}/{{scaling}}_seed{{seed}}/tree.nex",
        nexus_traits=f"{FOLDER}/RevBayes/{{simulator}}/{{scaling}}_seed{{seed}}/traits.nex"
    shell:
        'python3 {input.script} --input_tree {input.tree} --input_traits {input.traits} --output_tree {output.nexus_tree} --output_traits {output.nexus_traits}'

rule run_OU_RevBayes:
    input:
        exec=exec_dico['rb'],
        rev_file=f"{ROOT}/scripts/mcmc_{{rb}}_RJ.Rev",
        tree=rules.convert_to_RevBayes.output.nexus_tree,
        traits=rules.convert_to_RevBayes.output.nexus_traits
    output:
        log=f"{FOLDER}/RevBayes/{{simulator}}/{{scaling}}_seed{{seed}}/{{rb}}_RJ.log"
    params:
        folder=lambda wildcards: f"{FOLDER}/RevBayes/{wildcards.simulator}/{wildcards.scaling}_seed{wildcards.seed}"
    shell:
        'cd {params.folder} && {input.exec} {input.rev_file}'

rule compress_RevBayes_log:
    input:
        log=rules.run_OU_RevBayes.output.log
    output:
        log=f"{FOLDER}/RevBayes/{{simulator}}/{{scaling}}_seed{{seed}}/{{rb}}_RJ.log.gz"
    params:
        prefix=f"{FOLDER}/RevBayes/{{simulator}}/{{scaling}}_seed{{seed}}/{{rb}}_RJ"
    shell:
        'for f in {params.prefix}*; do if [ ! -f $f.gz ]; then gzip $f; fi; done'


rule gather_RevBayes_log:
    input:
        script=f"{ROOT}/scripts/plot_simulations_RevBayes.py",
        rb_log=expand(rules.compress_RevBayes_log.output.log,scaling=REPLICATES,seed=SEEDS,simulator=config["simulators"],rb=["simple_OU", "relaxed_OU"])
    output:
        plot=f"{FOLDER}/results/inference_RevBayes.tsv.gz"
    log:
        stdout=f"{FOLDER}/results/inference_bayesian.log"
    shell:
        'python3 {input.script} --folder {FOLDER}/RevBayes --output {output.plot} 1> {log.stdout}'