import os

ROOT = os.path.abspath('..')
FOLDER = os.path.abspath('.')

BURN_IN, UNTIL = 1000, 2000
datasets = ["NeutralPhyloP", "Primates"]
sexes = ["m", "f"]
logTs = [True]

bayescode_path = {}
for executable in ["nodetraits", "readnodetraits"]:
    exec_path = os.path.join(ROOT,f'utils/BayesCode/bin/{executable}')
    if not os.path.exists(exec_path):
        # Find executable in the path using whereis. If not found, raise an error.
        split = os.popen(f'whereis {executable}').read().split()
        if len(split) > 1:
            exec_path = split[1].strip()
        else:
            raise FileNotFoundError(f'{executable} not found. Please install BayesCode and add it to your path.')
    bayescode_path[executable] = exec_path
    print(f"Found {executable} at {exec_path}")


def data_dS(dataset, empirical=True):
    if empirical:
        dico_dS = {"E": "Edempdnds.dsom.tre",
                   "Zoo": "Zoo739_empiricaldnds.dsom.tre"}
    else:
        dico_dS = {"E": "Ed708genebrdnds.dsom.tre",
                   "Zoo": "Zoo739_sub1to5_genebranchdnds.dsom.tre"}
    dico_dS["Primates"] = "science.abn7829.primates/science.abn7829_data_s3.nw.tree"
    dico_dS["PrimatesNeutral"] = "science.abn7829.primates/science.abn7829_data_s3.scaled.nw.tree"
    dico_dS["Zoonomia"] = "241-mammalian-2020v2.phast-242.nh"
    dico_dS["Consensus30"] = "science.abl8189.Timescale/PreliminaryTopologies/X_lessGC40_241.tree"
    dico_dS["Consensus75"] = "science.abl8189.Timescale/PreliminaryTopologies/200Mammals_X_lessGC40_75consensus.tr"
    dico_dS[
        "NeutralPhyloP"] = "science.abl8189.Timescale/Table_S2_RootedTrees/Concatenation_HRA_neutral_241_10miss_rooted.tree"
    return dico_dS[dataset]


def data_pS(dataset):
    dico_pS = {"E": "EdtableTHV_158sp",
               "Zoo": "ZootableTHV_180Zoonomia_8traits_7287genes_withgc3andHzoo"}
    for k in ["Zoonomia", "Consensus30", "Consensus75", "NeutralPhyloP"]:
        dico_pS[k] = "science.abn5856.Ne/science.abn5856_table_s1.csv"
    for k in ["Primates", "PrimatesNeutral"]:
        dico_pS[k] = "science.abn7829.primates/science.abn7829_data_s2.csv"
    return dico_pS[dataset]


rule all:
    input: f"{FOLDER}/results/merge.tsv"

rule scale_primate_tree:
    input:
        script=f"{ROOT}/scripts/scale_tree.py",
        primate_tree=f"{FOLDER}/data/{data_dS('Primates')}",
        mammal_neutral_tree=f"{FOLDER}/data/{data_dS('NeutralPhyloP')}",
    output:
        tree=f"{FOLDER}/data/{data_dS('PrimatesNeutral')}"
    shell:
        'python3 {input.script} --primate_tree {input.primate_tree} --mammal_neutral_tree {input.mammal_neutral_tree} --output_tree {output.tree}'


rule pre_processed_traits:
    input:
        script=f"{ROOT}/scripts/pre_processed_traits_mammals.py",
        input_dS=lambda wildcards: f"{FOLDER}/data/{data_dS(wildcards.dataset,empirical=True)}",
        input_pS=lambda wildcards: f"{FOLDER}/data/{data_pS(wildcards.dataset)}",
        input_traits=f"{FOLDER}/data/Tsuboi_etal_NEE_mammal.csv",
    output:
        tree=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{sex}}_{{logT}}.tree",
        traits=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{sex}}_{{logT}}.traits.tsv",
        var_within=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{sex}}_{{logT}}.var_within.tsv"
    params:
        phenotype=lambda wildcards: f"--log_transform {wildcards.logT} --sex {wildcards.sex}"
    log:
        stdout=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{sex}}_{{logT}}.preprocessed.log"
    shell:
        'python3 {input.script} {params.phenotype} --input_dS {input.input_dS} --input_pS {input.input_pS}'
        ' --input_traits {input.input_traits} --output_tree {output.tree} --output_traits {output.traits}'
        ' --output_var_within {output.var_within} > {log.stdout}'

rule neutrality_index:
    input:
        script=f"{ROOT}/scripts/neutrality_index.py",
        tree=rules.pre_processed_traits.output.tree,
        traits=rules.pre_processed_traits.output.traits,
        var_within=rules.pre_processed_traits.output.var_within
    output:
        tsv=f"{FOLDER}/data_processed/ML/{{dataset}}_{{sex}}_{{logT}}.tsv"
    log:
        stdout=f"{FOLDER}/data_processed/ML/{{dataset}}_{{sex}}_{{logT}}.processed.log"
    shell:
        'python3 {input.script} --tree {input.tree} --traitsfile {input.traits} --var_within {input.var_within}'
        ' --output {output.tsv} > {log.stdout}'


rule run_inference:
    input:
        exec=bayescode_path["nodetraits"],
        traits=rules.pre_processed_traits.output.traits,
        tree=rules.pre_processed_traits.output.tree
    output:
        run=f"{FOLDER}/data_processed/inference/{{dataset}}_{{sex}}_{{logT}}.run",
    params:
        chain=f"{FOLDER}/data_processed/inference/{{dataset}}_{{sex}}_{{logT}}",
        until=f"--until {UNTIL}",
    log:
        stdout=f"{FOLDER}/data_processed/inference/{{dataset}}_{{sex}}_{{logT}}.log"
    shell:
        '{input.exec} {params.until} --uniq_kappa --df 1 --tree {input.tree} --traitsfile {input.traits} {params.chain}'
        ' > {log.stdout}'

rule read_trace:
    input:
        exec=bayescode_path["readnodetraits"],
        inference=rules.run_inference.output.run,
        var_within=rules.pre_processed_traits.output.var_within
    output:
        tsv=f"{FOLDER}/data_processed/traces/{{dataset}}_{{sex}}_{{logT}}.ratio.tsv",
    params:
        chain=f"{FOLDER}/data_processed/inference/{{dataset}}_{{sex}}_{{logT}}"
    log:
        stdout=f"{FOLDER}/data_processed/traces/{{dataset}}_{{sex}}_{{logT}}.read.log"
    shell:
        '{input.exec} -b {BURN_IN} --var_within {input.var_within} --output {output.tsv} {params.chain} > {log.stdout} && gzip -f {params.chain}.chain && gzip -f {params.chain}.trace'

rule merge_tsv:
    input:
        script=f"{ROOT}/scripts/merge_results_mammals.py",
        tsv_Bayes=expand(rules.read_trace.output.tsv,dataset=datasets,sex=sexes,logT=logTs),
        tsv_ML=expand(rules.neutrality_index.output.tsv,dataset=datasets,sex=sexes,logT=logTs),
    output:
        tsv=f"{FOLDER}/results/merge.tsv",
    shell:
        'python3 {input.script} --tsv_ML {input.tsv_ML} --tsv_Bayes {input.tsv_Bayes} --output {output.tsv}'
